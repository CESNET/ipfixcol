#
# Copyright (c) 2011 CESNET
#
# LICENSE TERMS
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
# 1. Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright
#    notice, this list of conditions and the following disclaimer in
#    the documentation and/or other materials provided with the
#    distribution.
# 3. Neither the name of the Company nor the names of its contributors
#    may be used to endorse or promote products derived from this
#    software without specific prior written permission.
#
# ALTERNATIVELY, provided that this notice is retained in full, this
# product may be distributed under the terms of the GNU General Public
# License (GPL) version 2 or later, in which case the provisions
# of the GPL apply INSTEAD OF those given above.
#
# This software is provided ``as is'', and any express or implied
# warranties, including, but not limited to, the implied warranties of
# merchantability and fitness for a particular purpose are disclaimed.
# In no event shall the company or contributors be liable for any
# direct, indirect, incidental, special, exemplary, or consequential
# damages (including, but not limited to, procurement of substitute
# goods or services; loss of use, data, or profits; or business
# interruption) however caused and on any theory of liability, whether
# in contract, strict liability, or tort (including negligence or
# otherwise) arising in any way out of the use of this software, even
# if advised of the possibility of such damage.
#
# $Id$
#

AC_PREREQ([2.60])

# Process this file with autoconf to produce a configure script.
AC_INIT([ipfixcol], m4_esyscmd([cut -f1 ./VERSION | tr -d '\n']))

PACKAGE_VERSION=`cut -f1 ./VERSION | tr -d '\n'`
AC_SUBST(PACKAGE_VERSION)

# Initialization
CFLAGS="-Wall"

RELEASE=1
AC_SUBST(RELEASE)

USERNAME=`git config --get user.name`
USERMAIL=`git config --get user.email`
AC_SUBST(USERNAME)
AC_SUBST(USERMAIL)

CONFIGURE_PWD=`pwd`
AC_SUBST(CONFIGURE_PWD)

# Check for pkg-config.
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
	AC_MSG_ERROR(['pkgconfig' not found.])
fi

# redhat is THE default distribution
DISTRO=redhat 

# Check for current distribution
AC_CHECK_PROG([LSB_RELEASE], [lsb_release], [yes], [no])
if test "x$LSB_RELEASE" = "xyes"; then
	DISTRO_RAW="`lsb_release -i | sed 's/.*ID:\s*\(.*\)/\1/'`"
	if test "$DISTRO_RAW" = "SUSE LINUX"; then
		DISTRO=suse
	fi
fi

# Check if distribution was specified manually
AC_ARG_WITH([distro],
	AC_HELP_STRING([--with-distro=DISTRO],[Compile for specific Linux distribution]),
	DISTRO=$withval,
	AC_MSG_NOTICE([Detected distribution: $DISTRO. Run with --with-distro=DISTRO to override])
)

# Add --with-libxml2-path parameter
AC_ARG_WITH([libxml2-path],
	AC_HELP_STRING([--with-libxml2-path=DIR],[Path prefix to Libxml2 library]))

# add --without-openssl parameter
AC_ARG_WITH([openssl],
	AC_HELP_STRING([--without-openssl],[disable TLS support]), [with_openssl=no], [with_openssl=yes])


############################ Check for programs ################################

# in case of cross-compilation, try to guess cross-compiler if not set
if test "x$target" != "x$host" -a -z "`echo $CC | grep -e '-gcc'`";
then
  AC_MSG_CHECKING(for cross-compiler)

  which ${program_prefix}gcc >/dev/null 2>&1 && CC=${program_prefix}gcc
  which ${target_cpu}-${target_os}-gcc >/dev/null 2>&1 \
  && CC=${target_cpu}-${target-os}-gcc
  which ${target_cpu}-${target_vendor}-${target_os}-gcc >/dev/null 2>&1 \
  && CC=${target_cpu}-${target_vendor}-${target_os}-gcc

  AC_MSG_RESULT($CC)
fi

# Check for architecture, only x86 and x86_64 are supported
if test -z "$target_cpu";
then
	AC_CHECK_PROG([UNAME], [uname], [yes], [no])
	if test "$UNAME" = "no"; then
		AC_MSG_ERROR(["uname not found!"])
	fi
	target_cpu=`uname -m`
fi
AC_MSG_CHECKING([for host architecture])
case $target_cpu in
    i?86 )
	AC_MSG_RESULT($target_cpu)
	;;
    x86_64 )
	libdir='${prefix}/lib64'
	AC_MSG_RESULT($target_cpu)
        ;;
    * )
        AC_MSG_ERROR([unsupported architecture $target_cpu])
        ;;
esac

# Check for doxygen
AC_CHECK_PROG(DOXYGEN, doxygen, doxygen)

# Check for rpmbuil
AC_CHECK_PROG(RPMBUILD, rpmbuild, rpmbuild)

# Check for xsltproc
AC_CHECK_PROG(XSLTPROC, xsltproc, xsltproc)
# and Docbook stylesheets for manpages
if test -n "$XSLTPROC"; then
	case $DISTRO in
		suse )
			if test -f /usr/share/xml/docbook/stylesheet/nwalsh5/current/manpages/docbook.xsl; then
				XSLTMANSTYLE="/usr/share/xml/docbook/stylesheet/nwalsh5/current/manpages/docbook.xsl"
				XSLTHTMLSTYLE="/usr/share/xml/docbook/stylesheet/nwalsh5/current/html/docbook.xsl"
				XSLTXHTMLSTYLE="/usr/share/xml/docbook/stylesheet/nwalsh5/current/xhtml/docbook.xsl"
				BUILDREQS="$BUILDREQS docbook5-xsl-stylesheets"
			elif test -f /usr/share/xml/docbook/stylesheet/nwalsh/current/manpages/docbook.xsl; then
				XSLTMANSTYLE="/usr/share/xml/docbook/stylesheet/nwalsh/current/manpages/docbook.xsl"
				XSLTHTMLSTYLE="/usr/share/xml/docbook/stylesheet/nwalsh/current/html/docbook.xsl"
				XSLXTMLSTYLE="/usr/share/xml/docbook/stylesheet/nwalsh/current/xhtml/docbook.xsl"
				BUILDREQS="$BUILDREQS docbook-xsl-stylesheets"
			else
				AC_MSG_ERROR(["Docbook XSL stylesheet for man pages not found!"])
			fi
			;;
		redhat )
			if test -f /usr/share/sgml/docbook/xsl-stylesheets/manpages/docbook.xsl; then
				XSLTMANSTYLE="/usr/share/sgml/docbook/xsl-stylesheets/manpages/docbook.xsl"
				XSLTHTMLSTYLE="/usr/share/sgml/docbook/xsl-stylesheets/html/docbook.xsl"
				XSLTXHTMLSTYLE="/usr/share/sgml/docbook/xsl-stylesheets/xhtml/docbook.xsl"
				BUILDREQS="$BUILDREQS docbook-style-xsl"
			else
				AC_MSG_ERROR(["Docbook XSL stylesheet for man pages not found!"])
			fi
			;;
		* )
			AC_MSG_ERROR([Unsupported Linux distribuition])
			;;
	esac

	# and path to CSS for HTML
	# TODO: find some usefull style and use it here
	#MANHTMLCSS="--stringparam html.stylesheet http://linuxmanpages.com/global/main.css"
fi
AC_SUBST(XSLTHTMLSTYLE)
AC_SUBST(XSLTXHTMLSTYLE)
AC_SUBST(XSLTMANSTYLE)
AC_SUBST(MANHTMLCSS)

# Check for standard programs
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET

############################ Check for libraries ###############################

### LibXML2 ###
# Test whether Libxml2 exist
if test -n "$with_libxml2_path" ; then
	LIBS_LIB_XML="-lxml2"
	LDFLAGS_LIB_XML="-L$with_libxml2_path/lib"
else
	AC_CHECK_LIB([xml2], [main],
		[ LIBS_LIB_XML="-lxml2"],
		[ echo "Libxml2 not found - exiting! (use --with-libxml2-path=DIR if you has it in some other directory)" ; exit 1 ])
fi
AC_CHECK_PROG([XML2_CONFIG], [xml2-config], ["yes"], ["no"])
if test "$XML2_CONFIG" = "no"; then
	echo "xml2-config not found - exiting!"
	exit 1
fi
CFLAGS="$CFLAGS `xml2-config --cflags`"
LIBS="$LIBS $LIBS_LIB_XML"
LDFLAGS="$LDFLAGS $LDFLAGS_LIB_XML"

### TLS support ###
AS_IF([test "x$with_openssl" != xno],
  [AC_CHECK_LIB([ssl], [SSL_library_init],
    [AC_SUBST([TLS_SUPPORT], ["yes"])],
       AC_MSG_FAILURE([Required library openssl is missing])
  )], [AC_SUBST([TLS_SUPPORT], [no])])

### libcommlbr ###
AC_ARG_WITH([libcommlbr],
    	AC_HELP_STRING([--with-libcommlbr=PATH],[Set the path to libcommlbr (usually local GIT repository)]),
	[# local copy of libcommlbr GIT repo with headers and binaries
 	CONFIGURE_PARAMS="$CONFIGURE_PARAMS --with-libcommlbr=`cd $withval;pwd`"
	LIBCOMMLBR_CFLAGS="-I$withval"
	LIBCOMMLBR_LIBS="-L$withval -lcommlbr"],
	[# get info from pkg-config about installed package
 	PKG_CHECK_MODULES(LIBCOMMLBR, libcommlbr >= 2.0.0)]
)
CPPFLAGS="$CPPFLAGS $LIBCOMMLBR_CFLAGS"
LIBS="$LIBS $LIBCOMMLBR_LIBS"
AC_SUBST([LIBCOMMLBR_LIBS])
AC_SUBST([LIBCOMMLBR_CFLAGS])
BUILDREQS="$BUILDREQS libcommlbr-devel >= 2.0.0"
REQS="$REQS libcommlbr >= 2.0.0"

### pthread ###
AC_CHECK_LIB([pthread], [pthread_create], 
        [CFLAGS="$CFLAGS -pthread"],
    	AC_MSG_ERROR([Required library pthread missing]))

### dynamic linker ###
AC_CHECK_LIB([dl], [dlopen],,
        AC_MSG_ERROR([Required library dl missing]))

###################### Check for configure parameters ##########################
AC_ARG_ENABLE([debug], 
        AC_HELP_STRING([--enable-debug],[turn on more debugging options]),
        [CFLAGS="$CFLAGS -Wextra -g -pg -DDEBUG"])
        
AC_ARG_ENABLE([sctp], 
        AC_HELP_STRING([--disable-sctp],[disable sctp input plugin]),
        [SCTP=no], 
        [SCTP=yes])
        
AC_ARG_ENABLE([postgres], 
        AC_HELP_STRING([--enable-postgres],[enable PostgreSQL storage plugin]),
        [POSTGRES=yes],
        [POSTGRES=no])

AC_ARG_ENABLE([fastbit], 
        AC_HELP_STRING([--enable-fastbit],[enable fastbit storage plugin]),
        [FASTBIT=yes],
        [FASTBIT=no])

######################### Checks for header files ##############################
AC_CHECK_HEADERS([arpa/inet.h fcntl.h netdb.h netinet/in.h stdint.h stdlib.h string.h sys/ioctl.h sys/socket.h sys/time.h syslog.h unistd.h])

if test "$SCTP" = "yes"; then
	AC_CHECK_HEADER([netinet/sctp.h], , 
		[AC_MSG_ERROR([netinet/sctp.h header not found, run with --disable-sctp to build without SCTP support])]
	)
fi

if test "$POSTGRES" = "yes"; then
	AC_CHECK_HEADER([libpq-fe.h], , 
		[AC_MSG_ERROR([libpq-fe.h header not found])]
	)
fi

if test "$FASTBIT" = "yes"; then
	# this is C++ header file
	AC_LANG_PUSH([C++])
	AC_CHECK_HEADER([fastbit/ibis.h], , 
		[AC_MSG_ERROR([fastbit/ibis.h header not found])]
	)
	AC_LANG_POP([C++])
fi

if test "$with_openssl" != "no"; then
	AC_CHECK_HEADER([openssl/ssl.h], , 
		[AC_MSG_ERROR([openssl/ssl.h header not found])]
	)
fi

######## Checks for typedefs, structures, and compiler characteristics #########
AC_HEADER_STDBOOL
AC_C_INLINE
AC_TYPE_INT32_T
AC_TYPE_INT64_T
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_SSIZE_T
AC_TYPE_UINT16_T
AC_TYPE_UINT32_T
AC_TYPE_UINT64_T
AC_TYPE_UINT8_T

######################## Checks for library functions ##########################
AC_FUNC_MALLOC
AC_FUNC_MKTIME
AC_FUNC_MMAP
AC_FUNC_REALLOC
AC_FUNC_STRTOD
AC_CHECK_FUNCS([alarm atexit gethostbyname gettimeofday isascii localtime_r memset mkdir munmap putenv select setenv socket sqrt strcasecmp strchr strerror strtol strtoul])

########################## Check for plugins ###################################
# input plugins are located in src/input/
for dir in src/input/*; do
    if test -d "$dir" && test -f "$dir"/Makefile.in; then
    	if test ! "$dir" = "src/input/sctp" || test ! "$SCTP" = "no"; then
        	INPUT_PLUGINS="$INPUT_PLUGINS $dir";
        fi;
    fi;
done;

# storage plugins are located in src/storage
for dir in src/storage/*; do
    if test -d "$dir" && test -f "$dir"/Makefile.in; then
    	if test ! "$dir" = "src/storage/postgres" || test ! "$POSTGRES" = "no"; then
    		if test ! "$dir" = "src/storage/fastbit" || test ! "$FASTBIT" = "no"; then
        		STORAGE_PLUGINS="$STORAGE_PLUGINS $dir";
		fi;
        fi;
    fi;
done;


AC_SUBST(INPUT_PLUGINS)
AC_SUBST(STORAGE_PLUGINS)

############################# Check for tests ##################################
# tests are located in devel-tools/tests/
cd devel-tools/tests/;
for dir in *; do
    if test -d "$dir" -a -f "$dir"/Makefile.in; then
        TESTS="$TESTS $dir";
    fi;
done;
cd -;

AC_SUBST(TESTS)

############################# Check for tools ##################################
cd tools/;
for dir in *; do
    if test -d "$dir" -a -f "$dir"/Makefile.in; then
        TOOLS="$TOOLS $dir";
    fi;
done;
cd -;

AC_SUBST(TOOLS)

############################### Set output #####################################
AC_SUBST(DISTRO)
AC_SUBST(CONFIGURE_PARAMS)

AC_SUBST(DOXYGEN)
if test -z "$DOXYGEN"; then
	AC_MSG_WARN([Due to missing doxygen you will not able to generate Doxygen documentation.])
fi

AC_SUBST(RPMBUILD)
if test -z "$RPMBUILD"; then
	AC_MSG_WARN([Due to missing rpmbuild you will not able to generate RPM package.])
fi

AC_SUBST(XSLTPROC)
if test -z "$XSLTPROC"; then
	AC_MSG_WARN([Due to missing xsltproc you will not able to generate MAN pages.])
fi

# generate output
AC_CONFIG_FILES(Makefile)
AC_CONFIG_FILES(ipfixcol.spec)
AC_CONFIG_FILES(ipfixcol.pc)
# plugins makefiles
for i in $INPUT_PLUGINS $STORAGE_PLUGINS; do
    AC_CONFIG_FILES([$i/Makefile])
done;
# tests makefiles
for i in $TESTS; do
    AC_CONFIG_FILES([devel-tools/tests/$i/Makefile])
done;
# tools makefiles

AC_OUTPUT
